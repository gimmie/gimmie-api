apply plugin: 'java'
apply plugin: 'maven'

configurations {
  deployerJars
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.json:json:20140107'
  compile 'org.scribe:scribe:1.3+'
  compile 'org.apache.httpcomponents:httpclient:4.3+'
  compile 'org.apache.httpcomponents:httpcore:4.3+'
  deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

sourceSets {
  main {
    java {
      srcDir 'src'
    }
    resources {
      srcDir 'resources'
    }
  }

  test {
    java {
      srcDir 'test'
    }
    resources {
      srcDir 'test/resources'
    }
  }
}

task release (dependsOn: 'build') << {
  copy {
    from configurations.compile.files
    into "$buildDir/releases"
  }

  copy {
    from "$buildDir/libs"
    into "$buildDir/releases"
  }
}

defaultTasks "release"

group = 'com.gimmie'
version = '1.0'

uploadArchives {
  def username = System.getenv "DEPLOY_USER"
  def password = System.getenv "DEPLOY_PASSWORD"

  assert username != null, "DEPLOY_USER is required"
  assert password != null, "DEPLOY_PASSWORD is required"

  repositories.mavenDeployer {
    configuration = configurations.deployerJars
    repository(url: "scp://www.gimmieworld.com/var/sites/developer.gimmieworld.com/releases/maven") {
      authentication userName: username, password: password
    }
  }
}
