apply plugin: 'java'
apply plugin: 'maven'

group = 'com.gimmie'
version = '1.0'

configurations {
  releaseMaven
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.json:json:20140107'
  compile 'org.scribe:scribe:1.3+'
  compile 'org.apache.httpcomponents:httpclient:4.3+'
  compile 'org.apache.httpcomponents:httpcore:4.3+'

  releaseMaven "org.apache.maven.wagon:wagon-ssh:2.2"
}

sourceSets {
  main {
    java {
      srcDir 'src'
    }
    resources {
      srcDir 'resources'
    }
  }

  test {
    java {
      srcDir 'test'
    }
    resources {
      srcDir 'test/resources'
    }
  }
}

task release (dependsOn: 'build') << {
  copy {
    from configurations.compile.files
    into "$buildDir/releases"
  }

  copy {
    from "$buildDir/libs"
    into "$buildDir/releases"
  }
}

defaultTasks "release"

uploadArchives {
  def username = System.getenv "DEPLOY_USER"
  def password = System.getenv "DEPLOY_PASSWORD"
  def releaseUrl = System.getenv "DEPLOY_URL"

  assert username != null, "DEPLOY_USER is required"
  assert password != null, "DEPLOY_PASSWORD is required"
  assert releaseUrl != null, "DEPLOY_URL is required"

  repositories.mavenDeployer {
    configuration = configurations.releaseMaven
    repository(url: releaseUrl) {
      authentication userName: username, password: password
    }
  }
}
