apply plugin: 'java'
apply plugin: 'maven'

group = 'com.gimmie'
version = '1.0.5'

configurations {
  releaseMaven
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.json:json:20140107'
  compile 'org.scribe:scribe:1.3+'
  compile 'org.apache.httpcomponents:httpclient:4.3+'
  compile 'org.apache.httpcomponents:httpcore:4.3+'

  releaseMaven "org.apache.maven.wagon:wagon-ssh:2.2"
}

sourceSets {
  main {
    java {
      srcDir 'src'
    }
    resources {
      srcDir 'resources'
    }
  }

  test {
    java {
      srcDir 'test'
    }
    resources {
      srcDir 'test/resources'
    }
  }
}

task release (dependsOn: 'build') << {
  copy {
    from configurations.compile.files
    into "$buildDir/releases"
  }

  copy {
    from "$buildDir/libs"
    into "$buildDir/releases"
  }
}

task sourcesJar (type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

defaultTasks "release"

artifacts {
  archives sourcesJar
  archives jar
}

uploadArchives {
  repositories.mavenDeployer {
    configuration = configurations.releaseMaven
    repository(url: releaseUrl) {
      authentication userName: username, password: password
    }
  }
}
